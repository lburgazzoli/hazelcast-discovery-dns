// *****************************************************************************
//
// *****************************************************************************

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'osgi'
    id 'signing'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id "io.codearte.nexus-staging" version '0.9.0'
}

// *****************************************************************************
//
// *****************************************************************************

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
        boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'ea'].any {
            qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
        }
        if (rejected) {
            selection.reject('Release candidate')
        }
    }
  }
}

// *****************************************************************************
//
// *****************************************************************************

group               = 'com.github.lburgazzoli'
description         = 'An DNS based discovery mechanisms for Hazelcast'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    isReleaseVersion  = !version.endsWith("SNAPSHOT")
    isSnapshotVersion = version.endsWith("SNAPSHOT")
    isCI              = Boolean.valueOf("$System.env.CI")
    isPR              = System.env.CI_PULL_REQUEST?.trim() as Boolean
    gitRoot          = "https://github.com/lburgazzoli"
    gitProject       = "https://github.com/lburgazzoli/hazelcast-discovery-dns"
    gitURL           = "git@github.com/lburgazzoli/hazelcast-discovery-dns.git"
    gradleCiTasks    = (isReleaseVersion || isPR) ? ['clean', 'test'] : ['clean', 'test', 'uploadArchives']

    versions = [
        slf4j     : '1.7.25',
        hazelcast : '3.8.3',
        junit     : '4.12'
    ]

    bundleImportPackages = [
        '*'
    ]

    bundleExportPackages = [
        "*"
    ]

    if (isReleaseVersion) {
        nexusUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    } else {
        nexusUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// *************************************************************************
//
// *************************************************************************

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
    runtime("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }

    compile "com.hazelcast:hazelcast:$versions.hazelcast"

    testCompile("org.slf4j:slf4j-simple:$versions.slf4j") { transitive = false }
    testCompile "junit:junit:$versions.junit"
}

test {
    testLogging {
        events          'started', 'passed', 'skipped', 'failed', 'standardOut', 'standardError' 
        exceptionFormat 'full'
    }
}

[compileJava, compileTestJava]*.options*.compilerArgs = [
    "-Xlint:unchecked",
    "-Xlint:deprecation",
]

// *************************************************************************
//
// *************************************************************************

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Bundle-Description'     ] = "${project.name}"
        attributes['Bundle-Name'            ] = "${project.name}"
        attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.name}"
        attributes['Bundle-Version'         ] = project.version.replaceAll('-','.')
        attributes['Implementation-Title'   ] = "${project.group}-${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'

        instruction 'Import-Package' , bundleImportPackages.join(',')
        instruction 'Export-Package' , bundleExportPackages.join(',')
    }
}

// *****************************************************************************
// ARTIFACTS
// *****************************************************************************

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// *****************************************************************************
// Publish to Sonatype OSS
// *****************************************************************************

signing {
    required {
        isReleaseVersion
    }

    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: project.nexusUrl) {
                authentication(
                    userName: project.nexusUsername,
                    password: project.nexusPassword
                )
            }

            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            pom.groupId = project.group
            pom.artifactId = project.name
            pom.version = project.version

            pom.project {
                name project.name
                description project.name
                url project.gitProject

                scm {
                    url project.gitProject
                    connection "scm:${project.gitProject}"
                    developerConnection "scm:${project.gitURL}"
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lburgazzoli'
                        name 'Luca Burgazzoli'
                    }
                }
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn uploadArchives

task ci(dependsOn: project.gradleCiTasks) {
    doFirst {
        println "=============================================================="
        println "CI Tasks: ${project.gradleCiTasks}"
        println "=============================================================="
    }
}

task close(dependsOn: 'closeAndPromoteRepository') {
}
